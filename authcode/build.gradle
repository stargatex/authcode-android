import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    namespace 'com.stargatex.auth.authcode'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    resourcePrefix ="stargatex_auth_"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions.freeCompilerArgs += ["-Xexplicit-api=strict"]
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.7.0"


    implementation("com.squareup.okio:okio:3.2.0")
    implementation "com.google.code.gson:gson:2.9.0"
    implementation 'net.openid:appauth:0.11.1'
}

afterEvaluate {
    publishing {
        publications{
            library(MavenPublication){
                setGroupId 'com.github.stargatex.auth'
                setArtifactId 'authcode'
                version '1.0.0'
                artifact bundleReleaseAar

                pom {
                    name = 'authcode'
                    description = 'Auth Code Android Client'
                    url = 'https://github.com/stargatex/authcode-android.git'
                    licenses {
                        license {
                            name = 'Apache License'
                            url = 'https://github.com/stargatex/authcode-android/blob/main/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            name = 'Lahiru J'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/stargatex/authcode-android.git'
                        developerConnection = 'scm:git:ssh://github.com/stargatex/authcode-android.git'
                        url = 'https://github.com/stargatex/authcode-android/tree/main'
                    }

                }
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each { dependency ->
                        final dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
}
